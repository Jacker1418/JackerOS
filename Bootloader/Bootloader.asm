[ORG 0x00]
[BITS 16]

SECTION .text

jmp 0x07C0:START

TOTAL_SECTOR_COUNT:	DW 1024

START:
	mov AX, 0x07C0
	MOV DS, AX
	MOV AX, 0XB800
	MOV ES, AX

	MOV AX, 0X0000
	MOV SS, AX
	MOV SP, 0XFFFE
	MOV BP, 0XFFFE

	MOV SI, 0

.SCREEN_CLEAR_LOOP:
	MOV BYTE [ES:SI], 0
	MOV BYTE [ES:SI+1], 0X0A

	ADD SI, 2
	CMP SI, 80 * 25 * 2
	JL .SCREEN_CLEAR_LOOP

	PUSH MESSAGE1
	PUSH 0
	PUSH 0
	CALL PRINT_MESSAGE
	ADD SP, 6

	PUSH IMAGE_LOADING_MESSAGE
	PUSH 1
	PUSH 0
	CALL PRINT_MESSAGE
	ADD SP, 6

RESET_DISK:

	MOV AX, 0
	MOV DL, 0
	INT 0X13
	JC HANDLE_DISK_ERROR

	MOV SI, 0X1000
	MOV ES, SI
	MOV BX, 0X0000
	MOV DI, WORD [TOTAL_SECTOR_COUNT]

READ_DATA:
	CMP DI, 0
	JE READ_END
	SUB DI, 0X1

	MOV AH, 0X02
	MOV AL, 0X01
	MOV CH, BYTE [TRACK_NUMBER]
	MOV CL, BYTE [SECTOR_NUMBER]
	MOV DH, BYTE [HEAD_NUMBER]
	MOV DL, 0X00
	INT 0X13
	JC HANDLE_DISK_ERROR

	ADD SI, 0X0020
	MOV ES, SI

	MOV AL, BYTE [SECTOR_NUMBER]
	ADD AL, 0X01
	MOV BYTE [SECTOR_NUMBER], AL
	CMP AL, 19
	JL READ_DATA

	XOR BYTE [HEAD_NUMBER], 0X01
	MOV BYTE [SECTOR_NUMBER], 0X01

	CMP BYTE [HEAD_NUMBER], 0X00
	JNE READ_DATA

	ADD BYTE [TRACK_NUMBER], 0X01
	JMP READ_DATA
READ_END:

	PUSH LOADING_COMPLETE_MESSAGE
	PUSH 1
	PUSH 20
	CALL PRINT_MESSAGE
	ADD SP, 6

	jmp 0x1000:0x0000

HANDLE_DISK_ERROR:
	PUSH DISK_ERROR_MESSAGE
	PUSH 1
	PUSH 20
	CALL PRINT_MESSAGE

	JMP $

PRINT_MESSAGE:
	PUSH BP
	MOV BP, SP

	PUSH ES
	PUSH SI
	PUSH DI
	PUSH AX
	PUSH CX
	PUSH DX

	MOV AX, 0XB800
	MOV ES, AX

	MOV AX, WORD [BP+6]
	MOV SI, 160
	MUL SI
	MOV DI, AX

	MOV AX, WORD [BP+4]
	MOV SI, 2
	MUL SI
	ADD DI, AX

	MOV SI, WORD [BP+8]

.MESSAGE_LOOP:

	MOV CL, BYTE [SI]
	CMP CL, 0
	JE .MESSAGE_END

	MOV BYTE [ES:DI], CL

	ADD SI, 1
	ADD DI, 2

	JMP .MESSAGE_LOOP

.MESSAGE_END:
	POP DX
	POP CX
	POP AX
	POP DI
	POP SI
	POP ES
	POP BP
	
	RET

MESSAGE1:	DB 'MINT64 OS Boot Loader Start', 0

DISK_ERROR_MESSAGE:	DB	'DISK ERROR', 0
IMAGE_LOADING_MESSAGE:	DB	'OS IMAGE LOADING', 0
LOADING_COMPLETE_MESSAGE:	DB	'COMPLETE', 0

SECTOR_NUMBER:	DB 0X02
HEAD_NUMBER:	DB 0X00
TRACK_NUMBER:	DB 0X00

TIMES 510 - ( $ - $$ )	DB 0X00


DB 0X55
DB 0XAA
